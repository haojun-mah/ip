plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.22.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java']
        }
    }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
    testImplementation group: 'org.junit.platform', name: 'junit-platform-suite', version: '1.10.0'
    String javaFxVersion = '17.0.7'

    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: 'linux'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'win'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'mac'
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: 'linux'
}

checkstyle {
    toolVersion = '10.12.4'
    configFile = file('config/checkstyle/checkstyle.xml')
    ignoreFailures = true
}

spotless {
    java {
        // Use Google Java Format with AOSP style (4-space indentation) 
        // which is closer to SE-EDU standards than default Google style
        googleJavaFormat('1.17.0').aosp()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        target 'src/main/java/**/*.java', 'src/test/java/**/*.java', 'test/java/**/*.java'
        
        // Import ordering according to SE-EDU standards
        importOrder('static ', 'java', 'javax', 'org', 'com', '')
    }
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"

        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    mainClass.set("audrey.Launcher")
}

shadowJar {
    archiveBaseName = "audrey"
    archiveClassifier = null
    destinationDirectory = file('src/builds/libs')
}

run {
    standardInput = System.in
    dependsOn 'spotlessApply'
}

compileJava.dependsOn spotlessApply
compileTestJava.dependsOn spotlessApply
